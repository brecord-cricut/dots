#!/usr/bin/env zsh

export NVM_DIR="$XDG_CONFIG_HOME/nvm"
export NVMRC_PATH="$XDG_CONFIG_HOME/nvmrc"
export NPM_CONFIG_USERCONFIG="$XDG_CONFIG_HOME/npmrc"

if ! whence -w toggle_nvm >/dev/null; then
  toggle_nvm() {
    mkdir -p "$NVM_DIR"
    if [ -f "$NVM_DIR/disabled" ]; then
      rm "$NVM_DIR/disabled"
      echo "nvm enabled"
    else
      touch "$NVM_DIR/disabled"
      echo "nvm disabled"
    fi
    source "$ZSH_CUSTOM/nvm.zsh"
    echo "Re-sourced $ZSH_CUSTOM/nvm.zsh"
  }
fi

if [ -f $NVM_DIR/disabled ]; then
  return
fi

if [ ! -d $NVM_DIR ]; then
  curl -o- https://raw.githubusercontent.com/nvm-sh/nvm/v0.40.3/install.sh | bash
  echo "node" >$NVMRC_PATH
fi

[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"                   # This loads nvm
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion" # This loads nvm bash_completion

autoload -U add-zsh-hook

load-nvmrc() {
  local nvmrc_path
  nvmrc_path="$(nvm_find_nvmrc)"

  if [ -n "$nvmrc_path" ]; then
    local nvmrc_node_version
    nvmrc_node_version=$(nvm version "$(cat "${nvmrc_path}")")

    if [ "$nvmrc_node_version" = "N/A" ]; then
      nvm install
    elif [ "$nvmrc_node_version" != "$(nvm version)" ]; then
      nvm use
    fi
  elif [ -n "$(PWD=$OLDPWD nvm_find_nvmrc)" ] && [ "$(nvm version)" != "$(nvm version default)" ]; then
    echo "Reverting to nvm default version"
    nvm use default
  fi
}

add-zsh-hook chpwd load-nvmrc
load-nvmrc
